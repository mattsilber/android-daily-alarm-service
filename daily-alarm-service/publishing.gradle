if (System.getenv("DAS_RELEASE_MODE") != "true") {
    return
}

apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven-publish'
apply plugin: 'signing'

ext {
    mavProjectName = "daily-alarm-service"
    mavLibraryDescription = 'A Daily Alarm Service system'
}

ext["signing.keyId"] = System.getenv("DAS_RELEASE_SIGNING_KEY_ID")
ext["signing.password"] = System.getenv("DAS_RELEASE_SIGNING_KEY_PASSWORD")
ext["signing.secretKeyRingFile"] = System.getenv("DAS_RELEASE_SIGNING_KEY_RING_FILE")

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

dokkaJavadoc.configure {
    outputDirectory.set(file("$buildDir/javadoc"))

    dokkaSourceSets {
        configureEach {
            includeNonPublic.set(false)
            skipDeprecated.set(false)
            reportUndocumented.set(true)
            skipEmptyPackages.set(true)
        }
    }
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifact sourcesJar
                artifact javadocJar

                groupId = mavPublishGroupId
                artifactId = mavProjectName
                version = mavLibraryVersion

                pom {
                    name = mavProjectName
                    description = mavLibraryDescription
                    url = mavSiteUrl
                    licenses {
                        license {
                            name = mavLicenseName
                            url = mavLicenseUrl
                        }
                    }
                    developers {
                        developer {
                            id = mavDeveloperId
                            name = mavDeveloperName
                            email = mavDeveloperEmail
                            organization = mavDeveloperOrg
                            organizationUrl = mavDeveloperOrgUrl
                        }
                    }
                    scm {
                        connection = mavScmConnection
                        developerConnection = mavScmDeveloperConnection
                        url = mavGitUrl
                    }
                }
            }
        }

        repositories {
            maven {
                name = 'mavenCentral'
                url = mavDeploymentRepo

                credentials {
                    username System.getenv("DAS_MAVEN_REPO_USERNAME")
                    password System.getenv("DAS_MAVEN_REPO_PASSWORD")
                }
            }
        }
    }

    signing {
        sign publishing.publications
    }
}